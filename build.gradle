/*
 * Copyright 2018 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'net.wooga.plugins' version '3.2.0'
    id 'net.wooga.snyk' version '0.10.0'
    id "net.wooga.snyk-gradle-plugin" version "0.2.0"
    id "net.wooga.cve-dependency-resolution" version "0.4.0"
}

group 'net.wooga.gradle'
description = 'a Unity 3d build helper.'

pluginBundle {
    website = 'https://wooga.github.io/atlas-unity/'
    vcsUrl = 'https://github.com/wooga/atlas-unity'
    tags = ['Unity3D']
}

gradlePlugin {
    plugins {
        unity {
            id = 'net.wooga.build-unity'
            displayName = 'Gradle Unity build plugin'
            description = 'This plugin provides tasks for exporting platform projects from Unity3D projects'
            implementationClass = 'wooga.gradle.build.unity.UnityBuildPlugin'
        }

        ios {
            id = 'net.wooga.build-unity-ios'
            displayName = 'Gradle Unity iOS build plugin'
            description = 'This plugin provides tasks for building exported iOS projects from Unity3D'
            implementationClass = 'wooga.gradle.build.unity.ios.IOSBuildPlugin'
        }
    }
}

github {
    repositoryName = "wooga/atlas-build-unity"
}

cveHandler {
    configurations("compileClasspath", "runtimeClasspath", "testCompileClasspath", "testRuntimeClasspath", "integrationTestCompileClasspath", "integrationTestRuntimeClasspath")
}

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
    implementation "net.wooga.gradle:dotnet-sonarqube:[0.4,0.5["
    implementation "net.wooga.gradle:unity:[3,4["
    implementation 'com.wooga.gradle:gradle-commons:[1,2['
    implementation "org.gradle:gradle-tooling-api:+"
    api 'net.wooga.gradle:secrets:1.0.0-rc.6'
    api 'software.amazon.awssdk:secretsmanager:[2.16,3['
    implementation 'xmlwise:xmlwise:1.2.11'
    implementation 'commons-codec:commons-codec:1.14'
    implementation 'com.wooga.xcodebuild:xcpretty:[1,2['
    implementation 'com.googlecode.plist:dd-plist:1.23'
    implementation 'org.apache.commons:commons-text:1.8'
    testImplementation('com.nagternal:spock-genesis:0.6.0') {
        // This library tries to pull groovy-all in version 2.4 which is not compatible with groovy-all 2.5
        // There is no newer version of this library. We should either fork it or get rid of the 
        // property tests alltogether.
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }

    testImplementation "org.yaml:snakeyaml:1.28"
    testImplementation "com.wooga.gradle:gradle-commons-test:[1,2["

    implementation('com.wooga.security:macos-security:[1,2[')

    //TODO: I have no idea why we need this now.
    implementation('org.apache.maven:maven-artifact:[3,4[')

    integrationTestImplementation('com.wooga.spock.extensions:spock-macos-keychain-extension:[1,2[')
    integrationTestImplementation('commons-io:commons-io:2.6')
}

