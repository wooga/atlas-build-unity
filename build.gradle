/*
 * Copyright 2018 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'net.wooga.plugins' version '1.1.0'
}

group 'net.wooga.gradle'
description = 'a Unity 3d build helper.'

pluginBundle {
    website = 'https://wooga.github.io/atlas-unity/'
    vcsUrl = 'https://github.com/wooga/atlas-unity'
    tags = ['Unity3D']

    plugins {
        unity {
            id = 'net.wooga.build-unity'
            displayName = 'Gradle Unity build plugin'
            description = 'This plugin provides tasks for exporting platform projects from Unity3D projects'
        }

        ios {
            id = 'net.wooga.build-unity-ios'
            displayName = 'Gradle Unity iOS build plugin'
            description = 'This plugin provides tasks for building exported iOS projects from Unity3D'
        }

        webgl {
            id = 'net.wooga.build-unity-webgl'
            displayName = 'Gradle Unity WebGL build plugin'
            description = 'This plugin provides tasks for building exported webGL projects from Unity3D'
        }
    }
}

github {
    repositoryName = "wooga/atlas-build-unity"
}

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url "https://dl.bintray.com/nitram509/jbrotli" }
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.gradle:gradle-tooling-api:+"
    compile 'gradle.plugin.net.wooga.gradle:atlas-unity:1.+'
    compile 'xmlwise:xmlwise:1.2.11'

    compile 'commons-codec:commons-codec:1.8'
    compile 'org.meteogroup.jbrotli:jbrotli:0.5.0'
    if (System.getProperty('os.name').toLowerCase().contains("mac")) {
        compile "org.meteogroup.jbrotli:jbrotli-native-darwin-x86-amd64:0.5.0"
    } else if (System.getProperty('os.name').toLowerCase().contains("win") && System.getProperty('os.arch') == "x86_64") {
        compile "org.meteogroup.jbrotli:jbrotli-native-win32-x86-amd64:0.5.0"
    } else if (System.getProperty('os.name').toLowerCase().contains("win") && System.getProperty('os.arch') == "x86") {
        compile "org.meteogroup.jbrotli:jbrotli-native-win32-x86:0.5.0"
    }

    testCompile 'org.apache.commons:commons-text:1.3'
    testCompile('com.nagternal:spock-genesis:0.6.0') {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
}
